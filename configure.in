#
# $RCSfile: configure.in,v $
# ----------------------------------------------------------------------
# Configuration rules for rwrite package
# ----------------------------------------------------------------------
# Created      : Wed Dec 14 02:39:53 1994 tri
# Last modified: Sat Apr 18 23:37:41 1998 tri
# ----------------------------------------------------------------------
# Copyright 1994, Tero Kivinen <kivinen@hut.fi>.
#
# Copyright © 1994-1998
# Timo J. Rinne <tri@iki.fi>
# All rights reserved.  See file COPYRIGHT for details.
#
# Address: Cirion oy, PO-BOX 250, 00121 Helsinki, Finland
# ----------------------------------------------------------------------
# Any express or implied warranties are disclaimed.  In no event
# shall the author be liable for any damages caused (directly or
# otherwise) by the use of this software.
#
# Please, send your patches to <tri@iki.fi>.
# ----------------------------------------------------------------------
# $Revision: 1.7 $
# $State: Exp $
# $Date: 1998/04/18 20:52:28 $
# $Author: tri $
# ----------------------------------------------------------------------
# $Log: configure.in,v $
# Revision 1.7  1998/04/18 20:52:28  tri
# New copyright in COPYRIGHT.
#
# Revision 1.6  1996/05/21 15:16:10  tri
# Fixed.
#
# Revision 1.5  1996/05/21 14:55:44  tri
# Few system dependent definitions.
#
# Revision 1.4  1996/05/17 12:31:28  tri
# SOCKS support from kivinen@iki.fi.
#
# Revision 1.3  1995/10/24 21:54:56  tri
# Added support for gnu libreadline.
#
# Revision 1.2  1995/06/11  15:27:33  tri
# Added fdopen.
#
# Revision 1.1  1994/12/14  00:46:16  tri
# Initial revision
#
# ----------------------------------------------------------------------
#

dnl Process this file with autoconf to produce a configure script.
AC_INIT(rwrite.c)

AC_CANONICAL_HOST
AC_SUBST(RWRITED_GROUP)
AC_SUBST(RWRITED_MODE)
AC_SUBST(BIN_OWNER)
AC_SUBST(BIN_GROUP)
AC_SUBST(MAN_OWNER)
AC_SUBST(MAN_GROUP)

BIN_OWNER=bin
BIN_GROUP=bin
MAN_OWNER=bin
MAN_GROUP=bin
RWRITED_MODE=2555

AC_ARG_WITH(cc, [  --with-cc				compile with cc], CC=cc)
AC_ARG_ENABLE(dont-tell-users, [  --enable-dont-tell-users	server gives user not in instead of no such user], AC_DEFINE(DO_NOT_TELL_USERS))
AC_ARG_ENABLE(dont-flush, [  --enable-dont-flush		do not flush stdin after failed message], AC_DEFINE(DONT_FLUSH_INPUT_IN_FAILURE))
AC_ARG_ENABLE(send-fhst, [  --enable-send-fhst			enabled sending of localhostname in protocol], AC_DEFINE(SEND_FHST))
AC_ARG_WITH(ttygroup, [  --with-ttygroup=group			set group of tty-devices], RWRITED_GROUP=$with_ttygroup)
AC_ARG_WITH(binowner, [  --with-binowner=owner			set owner of binaries], BIN_OWNER=$with_binowner)
AC_ARG_WITH(bingroup, [  --with-bingroup=group			set group of binaries], BIN_GROUP=$with_bingroup)
AC_ARG_WITH(manowner, [  --with-manowner=owner			set owner of manuals], MAN_OWNER=$with_manowner)
AC_ARG_WITH(mangroup, [  --with-mangroup=group			set group of manuals], MAN_GROUP=$with_mangroup)
AC_ARG_WITH(socks, [  --with-socks[=PATH]			compile with SOCKS support.], , with_socks=no)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL

AC_DEFINE_UNQUOTED(HOSTTYPE, "$host")

case "$host" in
  *-sgi-irix5*)
    # Irix stuff from snabb@niksula.hut.fi, tsurmacz@asic.ict.pwr.wroc.pl,
    # C.Martin@sheffield.ac.uk, raistlin@uni-paderborn.de
    no_libsocket=yes
    no_libsun=yes
    no_libnsl=yes
    ;;
  *-sgi-irix6*)
    # from d-champion@uchicago.edu
    no_libsocket=yes
    no_libnsl=yes
    no_libsun=yes
    if test "`uname -s`" = "IRIX64"; then
      CFLAGS="-32 $CFLAGS"
      LDFLAGS="-32 $LDFLAGS"
    fi
    ;;
  *-*-hpux*)
    # HPUX flags from jgotts@engin.umich.edu
    if test -z "$GCC"; then
      CFLAGS="$CFLAGS -Aa -D_HPUX_SOURCE"
    fi
    ;;
  *)
    ;;
esac

dnl Check for socks
AC_MSG_CHECKING(whether to support SOCKS)
case "$with_socks" in
	no)
		AC_MSG_RESULT(no)
		;;
	*)
		AC_MSG_RESULT(yes)
		AC_DEFINE(SOCKS)
		if test "x$with_socks" = "xyes"; then
			with_socks="-lsocks"
		fi
		LIBS="$with_socks $LIBS"
		AC_TRY_LINK([],
			[ Rconnect(); ],
			[],
			[ AC_MSG_ERROR(Could not find the $with_socks library.  You must first install socks.) ])
		;;
esac

dnl Checks for libraries.
AC_CHECK_LIB(nsl, main)
AC_CHECK_LIB(bsd, main)
AC_CHECK_LIB(inet, main)
AC_CHECK_LIB(socket, main)
AC_CHECK_LIB(readline, main)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h sys/file.h sys/ioctl.h sys/time.h unistd.h sys/filio.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_UID_T
AC_MSG_CHECKING([for ut_type field in struct utmp])
AC_TRY_COMPILE([#include <utmp.h>], [struct utmp u; u.ut_type++;], AC_MSG_RESULT(yes), AC_MSG_RESULT(no); AC_DEFINE(NO_UT_TYPE_IN_STRUCT_UTMP))

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNCS(geteuid getegid fchown fchmod fdopen)

if test -n RWRITED_GROUP
then
  AC_MSG_CHECKING([for tty group])
  if grep '^tty:' /etc/group > /dev/null
  then
    AC_MSG_RESULT(yes)
    RWRITED_GROUP=tty
  else
    AC_MSG_RESULT(no)
    AC_MSG_CHECKING([for terminal group])
    if grep '^terminal:' /etc/group > /dev/null
    then
      AC_MSG_RESULT(yes)
      RWRITED_GROUP=terminal
    else 
      AC_MSG_RESULT(no)
      AC_MSG_CHECKING([for rwrite group])
      if grep '^rwrite:' /etc/group > /dev/null
      then
        AC_MSG_RESULT(yes)
        RWRITED_GROUP=rwrite
      else
        AC_MSG_RESULT(no)
        AC_MSG_WARN([No tty, terminal or rwrite group, disabling sgid, see INSTALL.])
	RWRITED_GROUP=$BIN_GROUP
	RWRITED_MODE=555
      fi
    fi
  fi
fi

AC_OUTPUT(Makefile)

# EOF (configure.in)
